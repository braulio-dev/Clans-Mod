buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "https://plugins.gradle.org/m2/" }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url = "https://maven.minecraftforge.net/" }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = "https://plugins.gradle.org/m2/" }
    maven { url = 'https://maven.minecraftforge.net/' }
    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url = 'https://jitpack.io' }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'org.spongepowered.mixin'

version = project.version
group = "club.mineplex.clans"
archivesBaseName = "ClansMod"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

mixin {
    defaultObfuscationEnv 'searge'
    add sourceSets.main, "clansmod.refmap.json"
}

minecraft {
    version = project.forgeVersion
    runDir = 'run'
    mappings = project.mappings
}

jar {
    manifest {
        attributes([
                'FMLCorePlugin'           : 'club.mineplex.clans.asm.FMLLoadingPlugin',
                'FMLCorePluginContainsMod': 'true',
                "MixinConfigs"            : "clansmod.mixins.json",
                'ForceLoadAsMod'          : 'true',
                'TweakClass'              : 'org.spongepowered.asm.launch.MixinTweaker'
        ])
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveName = project.modName + "-" + project.minecraftVersion + "-" + version + ".jar"
    enabled = false
}

dependencies {

    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }

    implementation 'com.github.Vatuu:discord-rpc:1.6.2'
}

processResources {

    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

}

sourceSets {
    main { output.resourcesDir = output.classesDir }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}

shadowJar {
    archiveName = tasks.jar.archiveName
    classifier = ''
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude('LICENSE')
}

task copyLicenses(type: Copy) {
    from(project.projectDir) {
        include('LICENSE')
        include('NOTICE')
        include('licenses/**')
    }
    into(sourceSets.main.output.resourcesDir)
}

reobf {
    shadowJar {
        mappingType = 'searge'
    }
}

artifacts {
    archives jar
    archives shadowJar
}

tasks.jar.dependsOn(tasks.copyLicenses)
reobfJar.enabled = false